plugins {
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java-library'
    group = "net.woggioni"
    version = jwoVersion

    repositories {
        maven {
            url = 'https://woggioni.net/mvn/'
        }
        mavenCentral()
    }
    dependencies {
        ['', 'test'].each {sourceSetName ->
            ['compileOnly', 'annotationProcessor'].each { configurationSuffix ->
                String configurationName
                if(sourceSetName) configurationName = sourceSetName + configurationSuffix.capitalize()
                else configurationName = configurationSuffix
                add(configurationName,  [group: "org.projectlombok", name: "lombok", version: lombokVersion])
            }
        }
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    }
}

dependencies {
    implementation group: "org.slf4j", name: "slf4j-api", version: slf4jVersion
}

compileJava {
    options.release = 8
}

jar {
    manifest{
        attributes([
            "Automatic-Module-Name": "net.woggioni.jwo"
        ])
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = "7.0.2"
}

publishing {
    repositories {
        maven {
            url = 'https://mvn.woggioni.net/'
        }
    }
    publications {
        maven(MavenPublication) {
            from(components["java"])
        }
    }
}


